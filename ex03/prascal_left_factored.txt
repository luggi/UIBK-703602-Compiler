start                   : PROGRAM IDENT ; varDec compStmt .

varDec                  : VAR varDecList
                        | eps

varDecList              : identListType ; varDecList_

varDecList_             : varDecList
                        | eps

identListType           : identList : type

identList               : IDENT identList_

identList_              : , identList
                        | eps

type                    : simpleType
                        | ARRAY [ NUM .. NUM ] OF simpleType

simpleType              : INTEGER
                        | REAL
                        | BOOLEAN

compStmt                : BEGIN stmtList END

stmtList                : statement stmtList_

stmtList_               : ; stmtList
                        | eps

statement               : assignStmt
                        | compStmt
                        | ifStmt
                        | whileStmt
                        | forStmt
                        | READ ( exprList )
                        | WRITE ( exprList )

assignStmt              : IDENT assignStmt_

assignStmt_             : := expr
                        | [ expr ] := expr

ifStmt                  : IF expr THEN statement elsePart

elsePart                : ELSE statement
                        | eps

whileStmt               : WHILE expr DO statement

forStmt                 : FOR IDENT := expr toPart expr DO statement

toPart                  : TO
                        | DOWNTO

expr                    : simpleExpr expr_

expr_                   : relOp simpleExpr
                        | eps

exprList                : expr exprList_

exprList_               : , exprList
                        | eps

simpleExpr              : term simpleExpr_

simpleExpr_             : addOp simpleExpr
                        | eps

term                    : factor term_

term_                   : mulOp term
                        | eps

factor                  : NUM
                        | STRING
                        | FALSE
                        | TRUE
                        | IDENT factor_
                        | NOT factor
                        | - factor
                        | ( expr )

factor_                 : [ expr ]
                        | eps

relOp                   : <
                        | <=
                        | >
                        | >=
                        | =
                        | <>

addOp                   : +
                        | -
                        | OR

mulOp                   : *
                        | /
                        | DIV
                        | MOD
                        | AND
